plugins {
    id "java"
    id "groovy"
    id "org.jenkins-ci.jpi" version "0.43.0"
}

jenkinsPlugin {
    jenkinsVersion = (rootProject.file("Dockerfile").text =~ /JENKINS_VERSION=([0-9.]+)/)[0][1]
    fileExtension = "jpi"
}

test {
    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}

ext {
    // Pull versions out of the plugins.txt to use in dependency resolution
    // TODO: when we break out the plugin into a separate entity we will want minimum versions not synch'd versions
    PLUGIN_VERSIONS = file("resources/plugins.txt")
            .text
            .split("\n")
            .findAll { !it.trim().isEmpty() }
            .collectEntries { plugin ->
                String[] parts = plugin.split(":")
                return [(parts[0]): parts[1]]
            }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (!details.requested.version && PLUGIN_VERSIONS[details.requested.name]) {
            details.useVersion PLUGIN_VERSIONS[details.requested.name]
        }
    }
}

dependencies {
    api "org.jenkins-ci.plugins:branch-api"
    api "org.jenkins-ci.plugins:credentials-binding"
    api "org.jenkins-ci.plugins:github-branch-source"

    implementation "org.jenkins-ci.plugins:git"
    implementation "org.jenkins-ci.plugins.workflow:workflow-job"
    implementation "io.jenkins.plugins:pipeline-groovy-lib:593.va_a_fc25d520e9"
    implementation "org.jenkins-ci.plugins:pipeline-github"
    implementation "org.kohsuke:github-api:1.301"

    testCompile "org.spockframework:spock-core:1.3-groovy-2.4"
    testCompile "cglib:cglib-nodep:3.3.0"
    testCompile "org.objenesis:objenesis:3.2"
    testCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"

    testCompile "org.jenkins-ci.plugins.workflow:workflow-api:1138.v619fd5201b_2f"
    testCompile "org.jenkins-ci.plugins.workflow:workflow-basic-steps:2.24"
    testCompile "org.jenkins-ci.plugins.workflow:workflow-cps:2659.v52d3de6044d0"
    testCompile "org.jenkins-ci.plugins.workflow:workflow-scm-step:2.13"
    testCompile "org.jenkins-ci.plugins:cloudbees-folder:6.708.ve61636eb_65a_5"
    testCompile "org.jenkins-ci.plugins:token-macro:277.v7c8f82a_d66b_3"
    testCompile "org.jenkins-ci.modules:sshd:3.1.0"
}


